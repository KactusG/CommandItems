group 'me.yamakaja.commanditems'
version '2.4.5'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

processResources {
    filesMatching("**/plugin.yml") {
        filter {
            it.replace('@version@', project.getVersion())
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://repo.codemc.org/repository/maven-public/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
    maven { url "https://libraries.minecraft.net" }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT"

    compile "com.mojang:authlib:1.5.21"

    compile 'de.tr7zw:item-nbt-api-plugin:2.10.0'

    compile "org.bstats:bstats-bukkit:3.0.0"

    compile "co.aikar:acf-paper:0.5.0-SNAPSHOT"

    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.0"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.0"
}

shadowJar {
    baseName = 'CommandItems'

    relocate 'de.tr7zw', 'me.yamakaja.commanditems.lib.de.tr7zw'

    relocate 'org.bstats', 'me.yamakaja.commanditems.lib.org.bstats'

    relocate 'co.aikar.commands', 'me.yamakaja.commanditems.lib.co.aikar.commands'
    relocate 'org.intellij', 'me.yamakaja.commanditems.lib.org.intellij'
    relocate 'org.jetbrains', 'me.yamakaja.commanditems.lib.org.jetbrains'
    relocate 'net.jodah', 'me.yamakaja.commanditems.lib.net.jodah'

    relocate 'org.yaml.snakeyaml', 'me.yamakaja.commanditems.lib.org.yaml.snakeyaml'
    relocate 'com.fasterxml', 'me.yamakaja.commanditems.lib.com.fasterxml'
}

task install(type: Copy) {
    from("$rootDir/build/libs/") {
        include "**/CommandItems-*.jar"
    }
    rename("([^-]*).*\\.jar", '$1.jar')
    into "$rootDir/testenv/plugins/"
}
